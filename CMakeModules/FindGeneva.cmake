################################################################################
#
# This file is part of the Geneva library collection. The following license
# applies to this file:
#
# ------------------------------------------------------------------------------
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ------------------------------------------------------------------------------
#
# Note that other files in the Geneva library collection may use a different
# license. Please see the licensing information in each file.
#
################################################################################
#
# See the NOTICE file in the top-level directory of the Geneva library
# collection for a list of contributors and copyright information.
#
################################################################################

#
# This file is part of the Geneva library collection's build system.
# Its purpose is to find an installed Geneva and expose it to downstream
# projects. It is the MODULE-mode fallback for installations that do NOT
# carry the modern config-file package (GenevaConfig.cmake); when that package
# is present, prefer find_package(Geneva CONFIG REQUIRED).
#
# This module accepts the following variables as hints for the search:
#
# GENEVA_ROOT       - The Geneva installation prefix where to search for libraries and includes
# GENEVA_INCLUDEDIR - The directory where the Geneva includes may be found
# GENEVA_LIBRARYDIR - The directory where the Geneva libraries may be found
#
#
# This module defines the following IMPORTED targets (matching those exported by
# the config-file package, so downstream code can be agnostic about how Geneva
# was found):
#
# Geneva::common      - utilities (logging, threads, serialization helpers)
# Geneva::hap         - random number generation
# Geneva::courtier    - consumer-based parallelization framework
# Geneva::geneva      - core optimization library (links the three above)
#
# Linking Geneva::geneva transitively brings in the other three targets and the
# Geneva include directory.
#
# For backward compatibility the following variables are also defined:
#
# GENEVA_INCLUDE_DIR          - The directory below which the Geneva includes may be found
# GENEVA_LIBRARY_DIR          - The directory below which the Geneva libraries may be found
# GENEVA_COMMON_LIBRARY       - Path to the common library
# GENEVA_HAP_LIBRARY          - Path to the random number generation library
# GENEVA_COURTIER_LIBRARY     - Path to the parallel/distributed computing library
# GENEVA_OPTIMIZATION_LIBRARY - Path to the main geneva optimization library
# GENEVA_LIBRARIES            - The Geneva imported targets, in linking order
# GENEVA_LIBS                 - The bare library names (not paths) that were found
# GENEVA_VERSION              - The Geneva version found by this module
# GENEVA_TESTING              - TRUE if the libraries were built with testing support
# GENEVA_FOUND                - TRUE if all required Geneva components were found
#
# Note: the former GENEVA_INDIVIDUAL_LIBRARY is gone -- the geneva-individuals
# library was dissolved into the geneva library.
#
# Example usages: FIND_PACKAGE(Geneva MODULE) or FIND_PACKAGE(Geneva MODULE REQUIRED)
#

###############################################################################

# Module for handling standard arguments given through FIND_PACKAGE()
INCLUDE(FindPackageHandleStandardArgs)
INCLUDE(CMakeFindDependencyMacro)

###############################################################################
# Analyze first the recieved variables

# If the CMake variables are not set, we check and use the corresponding
# environment variables
IF ((NOT GENEVA_ROOT) AND ENV{GENEVA_ROOT})
	SET (GENEVA_ROOT $ENV{GENEVA_ROOT})
ENDIF ()
IF ((NOT GENEVA_INCLUDEDIR) AND ENV{GENEVA_INCLUDEDIR})
	SET (GENEVA_INCLUDEDIR $ENV{GENEVA_INCLUDEDIR})
ENDIF ()
IF ((NOT GENEVA_LIBRARYDIR) AND ENV{GENEVA_LIBRARYDIR})
	SET (GENEVA_LIBRARYDIR $ENV{GENEVA_LIBRARYDIR})
ENDIF ()

# The more specific variables take precedence over the more generic ones
IF (GENEVA_ROOT)
	IF (NOT GENEVA_INCLUDEDIR)
		SET (GENEVA_INCLUDEDIR "${GENEVA_ROOT}/include")
	ENDIF ()
	IF (NOT GENEVA_LIBRARYDIR)
		SET (GENEVA_LIBRARYDIR "${GENEVA_ROOT}/lib")
	ENDIF ()
ENDIF ()

###############################################################################
# Check if some default locations are available to be used as backup
# alternatives in case the hint variables are empty

FILE (GLOB _INSTALL_DIRS "/opt/geneva*")
FOREACH ( dir IN LISTS _INSTALL_DIRS )
	SET (_INSTALL_DIRS_INCL ${_INSTALL_DIRS_INCL} "${dir}/include")
	SET (_INSTALL_DIRS_LIB ${_INSTALL_DIRS_LIB} "${dir}/lib")
ENDFOREACH ()

###############################################################################
# Find the include directories. We search for a single include
# directory only and assume that the others can be found at the
# same level

SET (GENEVA_COMMON_HEADER_PATH "common/GGlobalDefines.hpp" )

# Enforce a search order, making sure we first search in the given
# location, and only if not found we search the normal system paths
IF (GENEVA_INCLUDEDIR)
	FIND_PATH (
		GENEVA_INCLUDE_DIR
		NAMES ${GENEVA_COMMON_HEADER_PATH}
		PATHS ${GENEVA_INCLUDEDIR} NO_DEFAULT_PATH
	)
ENDIF ()
FIND_PATH (
	GENEVA_INCLUDE_DIR
	NAMES ${GENEVA_COMMON_HEADER_PATH}
	PATHS ${_INSTALL_DIRS_INCL}
	PATH_SUFFIXES "geneva-opt"
)

###############################################################################
# Find the libraries and set the related variables

# Note: libraries are listed in order of decreasing dependencies, which
# allows to use the GENEVA_LIBRARIES variable for linking with all of
# them as a block without linking order issues (hopefully)
SET ( _GENEVA_LIB_NAMES
	"geneva"
	"courtier"
	"hap"
	"common"
)

UNSET (GENEVA_LIBS)
FOREACH ( name IN LISTS _GENEVA_LIB_NAMES )
	STRING (TOUPPER ${name} ucname)

	# Enforce a search order, making sure we first search in the given
	# location, and only if not found we search the normal system paths
	IF (GENEVA_LIBRARYDIR)
		FIND_LIBRARY (
			GENEVA_${ucname}_LIBRARY
			NAMES "gemfony-${name}"
			PATHS ${GENEVA_LIBRARYDIR} NO_DEFAULT_PATH
		)
	ENDIF ()
	FIND_LIBRARY (
		GENEVA_${ucname}_LIBRARY
		NAMES "gemfony-${name}"
		PATHS ${_INSTALL_DIRS_LIB}
	)

	IF (GENEVA_${ucname}_LIBRARY)
		SET (GENEVA_LIBS ${GENEVA_LIBS} "gemfony-${name}")
	ENDIF ()
ENDFOREACH ()

# We rather use a more readable variable name for the optimization library...
IF (GENEVA_GENEVA_LIBRARY)
	SET (GENEVA_OPTIMIZATION_LIBRARY ${GENEVA_GENEVA_LIBRARY})
ELSE ()
	SET (GENEVA_OPTIMIZATION_LIBRARY "GENEVA_OPTIMIZATION_LIBRARY-NOTFOUND")
ENDIF ()

IF (GENEVA_COMMON_LIBRARY)
	GET_FILENAME_COMPONENT (GENEVA_LIBRARY_DIR ${GENEVA_COMMON_LIBRARY} PATH)
ENDIF ()

###############################################################################
# Determine the Geneva version found

IF (GENEVA_INCLUDE_DIR)
	# Read the Geneva version from the component macros in GGlobalDefines.hpp, each of
	# the form '#define GENEVA_VERSION_MAJOR 1' (plain decimals, the single source of truth).
	FOREACH ( _comp MAJOR MINOR PATCH )
		FILE (
			STRINGS
			${GENEVA_INCLUDE_DIR}/${GENEVA_COMMON_HEADER_PATH}
			_RAW_VERSION_LINE
			REGEX "#define[ \t]+GENEVA_VERSION_${_comp}[ \t]"
			LIMIT_COUNT 1
			LIMIT_INPUT 10000
		)
		STRING (
			REGEX REPLACE ".*GENEVA_VERSION_${_comp}[ \t]+([0-9]+).*" "\\1"
			_VER_${_comp} "${_RAW_VERSION_LINE}"
		)
	ENDFOREACH ()

	IF ( DEFINED _VER_MAJOR AND DEFINED _VER_MINOR AND DEFINED _VER_PATCH )
		SET (GENEVA_VERSION "${_VER_MAJOR}.${_VER_MINOR}.${_VER_PATCH}")
	ENDIF ()
ENDIF ()

###############################################################################
# Determine if Geneva was built with testing support

# A Geneva built with testing support embeds Catch2-based scaffolding in the
# geneva library and references Catch2 symbols. We approximate the test build
# by probing the installed library for a Catch2 symbol; the consumer is then
# responsible for linking Catch2 (see the message below).
SET (GENEVA_TESTING "FALSE")
IF (GENEVA_GENEVA_LIBRARY)
	EXECUTE_PROCESS(
		COMMAND ${CMAKE_NM} -D --defined-only "${GENEVA_GENEVA_LIBRARY}"
		OUTPUT_VARIABLE _GENEVA_NM_OUT
		ERROR_QUIET
	)
	IF (NOT _GENEVA_NM_OUT)
		# CMAKE_NM may be unset for the active toolchain; fall back to plain nm.
		EXECUTE_PROCESS(
			COMMAND nm -D --defined-only "${GENEVA_GENEVA_LIBRARY}"
			OUTPUT_VARIABLE _GENEVA_NM_OUT
			ERROR_QUIET
		)
	ENDIF ()
	IF (_GENEVA_NM_OUT MATCHES "Catch")
		SET (GENEVA_TESTING "TRUE")
	ENDIF ()
ENDIF ()

###############################################################################
# Check that all required files and directories were found, and report.
# To declare Geneva FOUND we need at least the include dir and the 'common'
# library; VERSION_VAR enables version-aware find_package(Geneva <ver>).

FIND_PACKAGE_HANDLE_STANDARD_ARGS (
	Geneva
	REQUIRED_VARS
		GENEVA_INCLUDE_DIR
		GENEVA_LIBRARY_DIR
		GENEVA_COMMON_LIBRARY
		GENEVA_HAP_LIBRARY
		GENEVA_COURTIER_LIBRARY
		GENEVA_GENEVA_LIBRARY
	VERSION_VAR GENEVA_VERSION
)

###############################################################################
# Define the IMPORTED targets, mirroring those provided by the config package.
# The targets are wired up in dependency order (common <- hap <- courtier <-
# geneva), so that linking Geneva::geneva pulls in the rest transitively.

IF (GENEVA_FOUND)
	# Geneva exposes Boost through its public headers; re-find it so the
	# imported targets can carry the Boost include dirs and link libraries,
	# matching the behaviour of the config-file package. The component list
	# mirrors GENEVA_BOOST_LIBS in CommonGenevaBuild.cmake.
	FIND_DEPENDENCY (Boost 1.91 COMPONENTS
		filesystem
		json
		program_options
		regex
		serialization
		atomic
	)

	IF (NOT TARGET Geneva::common)
		ADD_LIBRARY (Geneva::common UNKNOWN IMPORTED)
		SET_TARGET_PROPERTIES (Geneva::common PROPERTIES
			IMPORTED_LOCATION "${GENEVA_COMMON_LIBRARY}"
			INTERFACE_INCLUDE_DIRECTORIES "${GENEVA_INCLUDE_DIR}"
			INTERFACE_COMPILE_FEATURES "cxx_std_23"
			INTERFACE_LINK_LIBRARIES "Boost::filesystem;Boost::json;Boost::program_options;Boost::regex;Boost::serialization;Boost::atomic"
		)
	ENDIF ()

	IF (NOT TARGET Geneva::hap)
		ADD_LIBRARY (Geneva::hap UNKNOWN IMPORTED)
		SET_TARGET_PROPERTIES (Geneva::hap PROPERTIES
			IMPORTED_LOCATION "${GENEVA_HAP_LIBRARY}"
			INTERFACE_INCLUDE_DIRECTORIES "${GENEVA_INCLUDE_DIR}"
			INTERFACE_LINK_LIBRARIES "Geneva::common"
		)
	ENDIF ()

	IF (NOT TARGET Geneva::courtier)
		ADD_LIBRARY (Geneva::courtier UNKNOWN IMPORTED)
		SET_TARGET_PROPERTIES (Geneva::courtier PROPERTIES
			IMPORTED_LOCATION "${GENEVA_COURTIER_LIBRARY}"
			INTERFACE_INCLUDE_DIRECTORIES "${GENEVA_INCLUDE_DIR}"
			INTERFACE_LINK_LIBRARIES "Geneva::hap"
		)
	ENDIF ()

	IF (NOT TARGET Geneva::geneva)
		ADD_LIBRARY (Geneva::geneva UNKNOWN IMPORTED)
		SET_TARGET_PROPERTIES (Geneva::geneva PROPERTIES
			IMPORTED_LOCATION "${GENEVA_GENEVA_LIBRARY}"
			INTERFACE_INCLUDE_DIRECTORIES "${GENEVA_INCLUDE_DIR}"
			INTERFACE_LINK_LIBRARIES "Geneva::courtier"
		)
	ENDIF ()

	# Backward-compatible aggregate variable, now holding imported targets in
	# dependency order rather than bare library paths.
	SET (GENEVA_LIBRARIES
		Geneva::geneva
		Geneva::courtier
		Geneva::hap
		Geneva::common
	)
ENDIF ()

###############################################################################
# Report the results

IF (GENEVA_FOUND AND NOT Geneva_FIND_QUIETLY)
	IF (GENEVA_TESTING)
		MESSAGE(STATUS "Geneva: built with testing support")
	ELSE ()
		MESSAGE(STATUS "Geneva: built without testing support")
	ENDIF ()
ENDIF ()

###############################################################################
# Done
